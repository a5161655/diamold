plugins {
    id 'fabric-loom' version "${loom_version}"
    id "org.jetbrains.kotlin.jvm" version "${kotlin_version}"
    id 'com.matthewprenger.cursegradle' version "${curse_gradle}"
    id "com.modrinth.minotaur" version "${minotaur}"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = mod_id
//noinspection GroovyAssignabilityCheck
version = "${project.mod_version}+${project.minecraft_version}"
//noinspection GroovyAssignabilityCheck
group = maven_group


repositories {
    maven { url = "https://maven.fabricmc.net/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"


    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:${fabric_kotlin_version}"

}

curseforge {
    def props = new Properties()
    file("curseforge.properties").withInputStream { props.load(it) }
    apiKey = props."api_key"
    //noinspection GroovyAssignabilityCheck
    project {
        id = curse_id
        changelogType = "markdown"
        changelog = file('changelog.md')
        releaseType = release_type
        addGameVersion "1.19"
        addGameVersion "1.19.1"
        addGameVersion "Fabric"
        addGameVersion "Java 17"

        relations {
            requiredDependency 'fabric-api'
            requiredDependency 'fabric-language-kotlin'
        }
        mainArtifact(remapJar)
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }
    //noinspection GroovyAssignabilityCheck
    options {
        forgeGradleIntegration = false
    }
}

modrinth {
    def props = new Properties()
    file("curseforge.properties").withInputStream { props.load(it) }
    token = props."modrinth"
    projectId = "w1QfAiDE"
    versionNumber = mod_version
    versionType = "${release_type}"
    uploadFile = remapJar
    gameVersions = ["1.19"]
    changelog = new File('changelog.md').text
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        required.project "fabric-language-kotlin"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    //noinspection GroovyAssignabilityCheck
    it.options.release = 17
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_id}" }
    }
}

compileKotlin.kotlinOptions.jvmTarget = "17"

// if minecraft cant be imported try running gradlew --refresh-dependencies